apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    def isCI = System.getenv("CI") == "true"

    compileSdkVersion 29
    defaultConfig {
        applicationId "me.jfenn.alarmio"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 21
        versionName "1.0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    if (isCI) {
        buildTypes.release.signingConfig = signingConfigs.release
        signingConfigs.release.storeFile = file("../key.jks")
        signingConfigs.release.storePassword = System.getenv("KEYSTORE_PASSWORD")
        signingConfigs.release.keyAlias = System.getenv("KEYSTORE_ALIAS")
        signingConfigs.release.keyPassword = System.getenv("KEYSTORE_ALIAS_PASSWORD")
    }

    // enable Jave 8 features
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task printVersionName {
	doLast {
		println android.defaultConfig.versionName
	}
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // support libraries
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'

    // media dependencies
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.3'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.11.3'

    // uncategorized libraries
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.afollestad:aesthetic:0.7.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'me.jahirfiquitiva:FABsMenu:1.1.4'
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'me.jfenn:timedatepickers:0.0.6'
    implementation 'me.jfenn:Attribouter:0.1.6'
    implementation 'me.jfenn:SlideActionView:0.0.2'
    implementation 'me.jfenn:SunriseSunsetView:0.0.3'
    implementation 'me.jfenn:AndroidUtils:0.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
}
